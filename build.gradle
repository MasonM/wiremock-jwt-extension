plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
    id "org.owasp.dependencycheck" version "7.1.0.1"
}

sourceCompatibility = 1.7
group = 'com.github.masonm'
archivesBaseName = 'wiremock-jwt-extension'
version = '0.9'

repositories {
    // Canonical URLs at https://central.sonatype.org/pages/consumers.html
    maven { url 'https://repo1.maven.org/maven2' }
}

jar.finalizedBy shadowJar

shadowJar {
    classifier = '' // overwrite jar
    relocate "com.fasterxml.jackson", 'com.github.masonm.com.fasterxml.jackson'
}

task standaloneJar (type: Jar) {
    classifier = 'standalone'

    manifest {
        attributes("Main-Class": "com.github.masonm.JwtExtensionStandalone")
    }

    from {
        (configurations.compile + configurations.shadow).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    with jar
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar, standaloneJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

if (!hasProperty('ossrhUsername') || !hasProperty('ossrhPassword')) {
    ext.ossrhUsername = ''
    ext.ossrhPassword = ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'wiremock-jwt-extension'
                packaging 'jar'
                description 'Wiremock request matcher and stub mapping transformer for JSON Web Tokens (JWT)'
                url 'https://github.com/MasonM/wiremock-jwt-extension'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id  'masonm'
                        name 'Mason Malone'
                    }
                }

                scm {
                    connection 'scm:git:git://github.com/MasonM/wiremock-jwt-extension.git'
                    developerConnection 'scm:git:ssh://github.com:MasonM/wiremock-jwt-extension.git'
                    url 'https://github.com/MasonM/wiremock-jwt-extension'
                }
            }
        }
    }
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.14.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.2'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.15'

    shadow group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '2.33.2'

    testCompile group: 'com.google.guava', name: 'guava', version: '18.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.jmock', name: 'jmock', version: '2.5.1'
    testCompile group: 'org.jmock', name: 'jmock-junit4', version: '2.5.1'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.2.3'
    testCompile group: 'com.toomuchcoding.jsonassert', name: 'jsonassert', version: '0.4.7'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.2'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.2', classifier: 'tests'
}
